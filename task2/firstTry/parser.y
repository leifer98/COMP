%{

#include "nodes.hpp"
#include "output.hpp"
#include <memory>

// Bison declarations
extern int yylineno;
extern int yylex();

void yyerror(const char*);

// Root of the AST, set by the parser and used by other parts of the compiler
std::shared_ptr<ast::Node> program;

using namespace std;

%}

// Configure Bison for better stack management
%define parse.error verbose

// Tokens
%token VOID INT BYTE BOOL TRUE FALSE IF RETURN WHILE BREAK CONTINUE SC COMMA ID NUM NUM_B PLUS MINUS MULTIPLY DIVIDE RELOP EQ NE STRING ERROR

// Precedence and associativity
%right ASSIGN
%left OR
%left AND
%left EQ NE
%left RELOP
%left PLUS MINUS
%left MULTIPLY DIVIDE
%right NOT
%left LPAREN RPAREN LBRACE RBRACE
%right ELSE

%%

// Grammar Rules

// 1. ğ‘ƒğ‘Ÿğ‘œğ‘”ğ‘Ÿğ‘ğ‘š â†’ ğ¹ğ‘¢ğ‘›ğ‘ğ‘ 
Program: Funcs { program = $1; };

// 2. ğ¹ğ‘¢ğ‘›ğ‘ğ‘  â†’ ğœ–
// 3. ğ¹ğ‘¢ğ‘›ğ‘ğ‘  â†’ ğ¹ğ‘¢ğ‘›ğ‘ğ·ğ‘’ğ‘ğ‘™ ğ¹ğ‘¢ğ‘›ğ‘ğ‘ 
Funcs:      /* epsilon */ { $$ = std::make_shared<ast::Funcs>(); }
            | FuncDecl Funcs { 
                auto funcs = std::dynamic_pointer_cast<ast::Funcs>($2);
                funcs->push_front(std::dynamic_pointer_cast<ast::FuncDecl>($1));
                $$ = funcs; 
            };

// 4. ğ¹ğ‘¢ğ‘›ğ‘ğ·ğ‘’ğ‘ğ‘™ â†’ ğ‘…ğ‘’ğ‘¡ğ‘‡ğ‘¦ğ‘ğ‘’ ğ¼ğ· ğ¿ğ‘ƒğ´ğ‘…ğ¸ğ‘ ğ¹ğ‘œğ‘Ÿğ‘šğ‘ğ‘™ğ‘  ğ‘…ğ‘ƒğ´ğ‘…ğ¸ğ‘ ğ¿ğµğ‘…ğ´ğ¶ğ¸ ğ‘†ğ‘¡ğ‘ğ‘¡ğ‘’ğ‘šğ‘’ğ‘›ğ‘¡ğ‘  ğ‘…ğµğ‘…ğ´ğ¶ğ¸
FuncDecl:   RetType ID LPAREN Formals RPAREN LBRACE Statements RBRACE { 
                $$ = std::make_shared<ast::FuncDecl>(
                std::dynamic_pointer_cast<ast::ID>($2), 
                std::dynamic_pointer_cast<ast::Type>($1), 
                std::dynamic_pointer_cast<ast::Formals>($4), 
                std::dynamic_pointer_cast<ast::Statements>($7)
                );
            };

// 5. ğ‘…ğ‘’ğ‘¡ğ‘‡ğ‘¦ğ‘ğ‘’ â†’ ğ‘‡ğ‘¦ğ‘ğ‘’
// 6. ğ‘…ğ‘’ğ‘¡ğ‘‡ğ‘¦ğ‘ğ‘’ â†’ ğ‘‰ğ‘‚ğ¼ğ·
RetType:    Type { $$ = std::dynamic_pointer_cast<ast::Type>($1); }
            | VOID { $$ = std::dynamic_pointer_cast<ast::Type>($1); };

// 7. ğ¹ğ‘œğ‘Ÿğ‘šğ‘ğ‘™ğ‘  â†’ ğœ–
// 8. ğ¹ğ‘œğ‘Ÿğ‘šğ‘ğ‘™ğ‘  â†’ ğ¹ğ‘œğ‘Ÿğ‘šğ‘ğ‘™ğ‘ ğ¿ğ‘–ğ‘ ğ‘¡
Formals:    /* epsilon */ { $$ = std::make_shared<ast::Formals>(); }
            | FormalsList { $$ = std::dynamic_pointer_cast<ast::Formals>($1); };

// 9. ğ¹ğ‘œğ‘Ÿğ‘šğ‘ğ‘™ğ‘ ğ¿ğ‘–ğ‘ ğ‘¡ â†’ ğ¹ğ‘œğ‘Ÿğ‘šğ‘ğ‘™ğ·ğ‘’ğ‘ğ‘™
// 10. ğ¹ğ‘œğ‘Ÿğ‘šğ‘ğ‘™ğ‘ ğ¿ğ‘–ğ‘ ğ‘¡ â†’ ğ¹ğ‘œğ‘Ÿğ‘šğ‘ğ‘™ğ·ğ‘’ğ‘ğ‘™ ğ¶ğ‘‚ğ‘€ğ‘€ğ´ ğ¹ğ‘œğ‘Ÿğ‘šğ‘ğ‘™ğ‘ ğ¿ğ‘–ğ‘ ğ‘¡
FormalsList: FormalDecl { 
                $$ = std::make_shared<ast::Formals>(
                    std::dynamic_pointer_cast<ast::Formal>($1)
                );
             }
            | FormalDecl COMMA FormalsList {
                auto formals = std::dynamic_pointer_cast<ast::Formals>($3);
                formals->push_front(std::dynamic_pointer_cast<ast::Formal>($1));
                $$ = formals; 
            };


// 11. ğ¹ğ‘œğ‘Ÿğ‘šğ‘ğ‘™ğ·ğ‘’ğ‘ğ‘™ â†’ ğ‘‡ğ‘¦ğ‘ğ‘’ ğ¼ğ·
FormalDecl: Type ID { 
                $$ = std::make_shared<ast::Formal>(
                    std::dynamic_pointer_cast<ast::ID>($2),
                    std::dynamic_pointer_cast<ast::Type>($1)
                ); 
            };

// 12. ğ‘†ğ‘¡ğ‘ğ‘¡ğ‘’ğ‘šğ‘’ğ‘›ğ‘¡ğ‘  â†’ ğ‘†ğ‘¡ğ‘ğ‘¡ğ‘’ğ‘šğ‘’ğ‘›ğ‘¡
// 13. ğ‘†ğ‘¡ğ‘ğ‘¡ğ‘’ğ‘šğ‘’ğ‘›ğ‘¡ğ‘  â†’ ğ‘†ğ‘¡ğ‘ğ‘¡ğ‘’ğ‘šğ‘’ğ‘›ğ‘¡ğ‘  ğ‘†ğ‘¡ğ‘ğ‘¡ğ‘’ğ‘šğ‘’ğ‘›ğ‘¡
Statements: Statement { 
                $$ = std::make_shared<ast::Statements>(std::dynamic_pointer_cast<ast::Statement>($1));
            }
            | Statements Statement { 
                auto stmts = std::dynamic_pointer_cast<ast::Statements>($1);
                stmts->push_back(std::dynamic_pointer_cast<ast::Statement>($2));
                $$ = stmts; 
            };  

// 14. ğ‘†ğ‘¡ğ‘ğ‘¡ğ‘’ğ‘šğ‘’ğ‘›ğ‘¡ â†’ ğ¿ğµğ‘…ğ´ğ¶ğ¸ ğ‘†ğ‘¡ğ‘ğ‘¡ğ‘’ğ‘šğ‘’ğ‘›ğ‘¡ğ‘  ğ‘…ğµğ‘…ğ´ğ¶ğ¸
// 15. ğ‘†ğ‘¡ğ‘ğ‘¡ğ‘’ğ‘šğ‘’ğ‘›ğ‘¡ â†’ ğ‘‡ğ‘¦ğ‘ğ‘’ ğ¼ğ· ğ‘†ğ¶
// 16. ğ‘†ğ‘¡ğ‘ğ‘¡ğ‘’ğ‘šğ‘’ğ‘›ğ‘¡ â†’ ğ‘‡ğ‘¦ğ‘ğ‘’ ğ¼ğ· ğ´ğ‘†ğ‘†ğ¼ğºğ‘ ğ¸ğ‘¥ğ‘ ğ‘†ğ¶
// 17. ğ‘†ğ‘¡ğ‘ğ‘¡ğ‘’ğ‘šğ‘’ğ‘›ğ‘¡ â†’ ğ¼ğ· ğ´ğ‘†ğ‘†ğ¼ğºğ‘ ğ¸ğ‘¥ğ‘ ğ‘†ğ¶
// 18. ğ‘†ğ‘¡ğ‘ğ‘¡ğ‘’ğ‘šğ‘’ğ‘›ğ‘¡ â†’ ğ¶ğ‘ğ‘™ğ‘™ ğ‘†ğ¶
// 19. ğ‘†ğ‘¡ğ‘ğ‘¡ğ‘’ğ‘šğ‘’ğ‘›ğ‘¡ â†’ ğ‘…ğ¸ğ‘‡ğ‘ˆğ‘…ğ‘ ğ‘†ğ¶
// 20. ğ‘†ğ‘¡ğ‘ğ‘¡ğ‘’ğ‘šğ‘’ğ‘›ğ‘¡ â†’ ğ‘…ğ¸ğ‘‡ğ‘ˆğ‘…ğ‘ ğ¸ğ‘¥ğ‘ ğ‘†ğ¶
// 21. ğ‘†ğ‘¡ğ‘ğ‘¡ğ‘’ğ‘šğ‘’ğ‘›ğ‘¡ â†’ ğ¼ğ¹ ğ¿ğ‘ƒğ´ğ‘…ğ¸ğ‘ ğ¸ğ‘¥ğ‘ ğ‘…ğ‘ƒğ´ğ‘…ğ¸ğ‘ ğ‘†ğ‘¡ğ‘ğ‘¡ğ‘’ğ‘šğ‘’ğ‘›ğ‘¡
// 22. ğ‘†ğ‘¡ğ‘ğ‘¡ğ‘’ğ‘šğ‘’ğ‘›ğ‘¡ â†’ ğ¼ğ¹ ğ¿ğ‘ƒğ´ğ‘…ğ¸ğ‘ ğ¸ğ‘¥ğ‘ ğ‘…ğ‘ƒğ´ğ‘…ğ¸ğ‘ ğ‘†ğ‘¡ğ‘ğ‘¡ğ‘’ğ‘šğ‘’ğ‘›ğ‘¡ ğ¸ğ¿ğ‘†ğ¸ ğ‘†ğ‘¡ğ‘ğ‘¡ğ‘’ğ‘šğ‘’ğ‘›ğ‘¡
// 23. ğ‘†ğ‘¡ğ‘ğ‘¡ğ‘’ğ‘šğ‘’ğ‘›ğ‘¡ â†’ ğ‘Šğ»ğ¼ğ¿ğ¸ ğ¿ğ‘ƒğ´ğ‘…ğ¸ğ‘ ğ¸ğ‘¥ğ‘ ğ‘…ğ‘ƒğ´ğ‘…ğ¸ğ‘ ğ‘†ğ‘¡ğ‘ğ‘¡ğ‘’ğ‘šğ‘’ğ‘›ğ‘¡
// 24. ğ‘†ğ‘¡ğ‘ğ‘¡ğ‘’ğ‘šğ‘’ğ‘›ğ‘¡ â†’ ğµğ‘…ğ¸ğ´ğ¾ ğ‘†ğ¶
// 25. ğ‘†ğ‘¡ğ‘ğ‘¡ğ‘’ğ‘šğ‘’ğ‘›ğ‘¡ â†’ ğ¶ğ‘‚ğ‘ğ‘‡ğ¼ğ‘ğ‘ˆğ¸ ğ‘†ğ¶
Statement:  LBRACE Statements RBRACE {
                $$ = std::dynamic_pointer_cast<ast::Statements>($2);
            } 
            | Type ID SC { 
                $$ = std::make_shared<ast::VarDecl>(
                    std::dynamic_pointer_cast<ast::ID>($2), // Variable name
                    std::dynamic_pointer_cast<ast::Type>($1) // Variable type
                );
            }
            | Type ID ASSIGN Exp SC { 
                $$ = std::make_shared<ast::VarDecl>(
                    std::dynamic_pointer_cast<ast::ID>($2), // Variable name
                    std::dynamic_pointer_cast<ast::Type>($1), // Variable type
                    std::dynamic_pointer_cast<ast::Exp>($4) // Initial value
                );
            }
            | ID ASSIGN Exp SC {
                $$ = std::make_shared<ast::Assign>(
                    std::dynamic_pointer_cast<ast::ID>($1), // Variable name
                    std::dynamic_pointer_cast<ast::Exp>($3) // Initial value
                );
            }
            | Call SC {
                $$ = std::dynamic_pointer_cast<ast::Call>($1);
            }
            | RETURN SC {
                $$ = std::make_shared<ast::Return>();
            }
            | RETURN Exp SC {
                $$ = std::make_shared<ast::Return>(
                    std::dynamic_pointer_cast<ast::Exp>($2)
                );
            }
            | IF LPAREN Exp RPAREN Statement {
                $$ = std::make_shared<ast::If>(
                    std::dynamic_pointer_cast<ast::Exp>($3), // Condition
                    std::dynamic_pointer_cast<ast::Statement>($5) // Body
                );
            }
            | IF LPAREN Exp RPAREN Statement ELSE Statement {
                $$ = std::make_shared<ast::If>(
                    std::dynamic_pointer_cast<ast::Exp>($3), // Condition
                    std::dynamic_pointer_cast<ast::Statement>($5), // Body
                    std::dynamic_pointer_cast<ast::Statement>($7) // Else body
                );
            }
            | WHILE LPAREN Exp RPAREN Statement {
                $$ = std::make_shared<ast::While>(
                    std::dynamic_pointer_cast<ast::Exp>($3), // Condition
                    std::dynamic_pointer_cast<ast::Statement>($5) // Body
                );
            }
            | BREAK SC {
                $$ = std::dynamic_pointer_cast<ast::Break>($1);
            }
            | CONTINUE SC {
                $$ = std::dynamic_pointer_cast<ast::Continue>($1);
            };

// 26. ğ¶ğ‘ğ‘™ğ‘™ â†’ ğ¼ğ· ğ¿ğ‘ƒğ´ğ‘…ğ¸ğ‘ ğ¸ğ‘¥ğ‘ğ¿ğ‘–ğ‘ ğ‘¡ ğ‘…ğ‘ƒğ´ğ‘…ğ¸ğ‘
// 27. ğ¶ğ‘ğ‘™ğ‘™ â†’ ğ¼ğ· ğ¿ğ‘ƒğ´ğ‘…ğ¸ğ‘ ğ‘…ğ‘ƒğ´ğ‘…ğ¸ğ‘
Call:       ID LPAREN ExpList RPAREN {
                $$ = std::make_shared<ast::Call>(
                    std::dynamic_pointer_cast<ast::ID>($1), // Function name
                    std::dynamic_pointer_cast<ast::ExpList>($3) // Arguments
                );
            }
            | ID LPAREN RPAREN {
                $$ = std::make_shared<ast::Call>(
                    std::dynamic_pointer_cast<ast::ID>($1), // Function name
                    std::make_shared<ast::ExpList>() // Arguments
                );
            };

// 28. ğ¸ğ‘¥ğ‘ğ¿ğ‘–ğ‘ ğ‘¡ â†’ ğ¸ğ‘¥ğ‘
// 29. ğ¸ğ‘¥ğ‘ğ¿ğ‘–ğ‘ ğ‘¡ â†’ ğ¸ğ‘¥ğ‘ ğ¶ğ‘‚ğ‘€ğ‘€ğ´ ğ¸ğ‘¥ğ‘ğ¿ğ‘–ğ‘ ğ‘¡
ExpList:    Exp {
                $$ = std::make_shared<ast::ExpList>(
                    std::dynamic_pointer_cast<ast::Exp>($1)
                );
            }
            | Exp COMMA ExpList {
                auto expList = std::dynamic_pointer_cast<ast::ExpList>($3);
                expList->push_front(std::dynamic_pointer_cast<ast::Exp>($1));
                $$ = expList; 
            };


// 30. ğ‘‡ğ‘¦ğ‘ğ‘’ â†’ ğ¼ğ‘ğ‘‡
// 31. ğ‘‡ğ‘¦ğ‘ğ‘’ â†’ ğµğ‘Œğ‘‡ğ¸
// 32. ğ‘‡ğ‘¦ğ‘ğ‘’ â†’ ğµğ‘‚ğ‘‚ğ¿
Type: INT { $$ = std::dynamic_pointer_cast<ast::Type>($1); }
    | BYTE { $$ = std::dynamic_pointer_cast<ast::Type>($1); }
    | BOOL { $$ = std::dynamic_pointer_cast<ast::Type>($1); };

// 33. ğ¸ğ‘¥ğ‘ â†’ ğ¿ğ‘ƒğ´ğ‘…ğ¸ğ‘ ğ¸ğ‘¥ğ‘ ğ‘…ğ‘ƒğ´ğ‘…ğ¸ğ‘
// 34. (ğ¸ğ‘¥ğ‘ â†’ Exp BINOP Exp) - Replaced with:
//     ğ¸ğ‘¥ğ‘ â†’ Exp PLUS Exp
//     ğ¸ğ‘¥ğ‘ â†’ Exp MINUS Exp
//     ğ¸ğ‘¥ğ‘ â†’ Exp MULTIPLY Exp
//     ğ¸ğ‘¥ğ‘ â†’ Exp DIVIDE Exp
// 35. ğ¸ğ‘¥ğ‘ â†’ ID
// 36. ğ¸ğ‘¥ğ‘ â†’ ğ¶ğ‘ğ‘™ğ‘™
// 37. ğ¸ğ‘¥ğ‘ â†’ ğ‘ğ‘ˆğ‘€
// 38. ğ¸ğ‘¥ğ‘ â†’ ğ‘ğ‘ˆğ‘€ ğµ
// 39. ğ¸ğ‘¥ğ‘ â†’ ğ‘†ğ‘‡ğ‘…ğ¼ğ‘ğº
// 40. ğ¸ğ‘¥ğ‘ â†’ ğ‘‡ğ‘…ğ‘ˆğ¸
// 41. ğ¸ğ‘¥ğ‘ â†’ ğ¹ğ´ğ¿ğ‘†E
// 42. ğ¸ğ‘¥ğ‘ â†’ ğ‘ğ‘‚ğ‘‡ ğ¸ğ‘¥ğ‘
// 43. ğ¸ğ‘¥ğ‘ â†’ ğ¸ğ‘¥ğ‘ ğ´ğ‘ğ· ğ¸ğ‘¥ğ‘
// 44. ğ¸ğ‘¥ğ‘ â†’ ğ¸ğ‘¥ğ‘ ğ‘‚ğ‘… ğ¸ğ‘¥ğ‘
// 45. (ğ¸ğ‘¥ğ‘ â†’ ğ¸ğ‘¥ğ‘ ğ‘…ğ¸ğ¿ğ‘‚ğ‘ƒ ğ¸ğ‘¥p) - Replaced with:
//     ğ¸ğ‘¥ğ‘ â†’ ğ¸ğ‘¥ğ‘ ğ‘…ğ¸ğ¿ğ‘‚ğ‘ƒ ğ¸ğ‘¥p
//     ğ¸ğ‘¥ğ‘ â†’ ğ¸ğ‘¥ğ‘ EQ ğ¸ğ‘¥p
//     ğ¸ğ‘¥ğ‘ â†’ ğ¸ğ‘¥ğ‘ NE ğ¸ğ‘¥p
// 46. ğ¸ğ‘¥ğ‘ â†’ ğ¿ğ‘ƒğ´ğ‘…ğ¸ğ‘ ğ‘‡ğ‘¦ğ‘ğ‘’ ğ‘…ğ‘ƒğ´ğ‘…ğ¸ğ‘ ğ¸ğ‘¥ğ‘
Exp:        LPAREN Exp RPAREN { $$ = $2; }
            | Exp PLUS Exp {
                $$ = std::make_shared<ast::BinOp>(
                    std::dynamic_pointer_cast<ast::Exp>($1), // Left operand
                    std::dynamic_pointer_cast<ast::Exp>($3), // Right operand
                    ast::BinOpType::ADD
                );
            }
            | Exp MINUS Exp {
                $$ = std::make_shared<ast::BinOp>(
                    std::dynamic_pointer_cast<ast::Exp>($1), // Left operand
                    std::dynamic_pointer_cast<ast::Exp>($3), // Right operand
                    ast::BinOpType::SUB
                );
            }
            | Exp MULTIPLY Exp {
                $$ = std::make_shared<ast::BinOp>(
                    std::dynamic_pointer_cast<ast::Exp>($1), // Left operand
                    std::dynamic_pointer_cast<ast::Exp>($3), // Right operand
                    ast::BinOpType::MUL
                );
            }
            | Exp DIVIDE Exp {
                $$ = std::make_shared<ast::BinOp>(
                    std::dynamic_pointer_cast<ast::Exp>($1), // Left operand
                    std::dynamic_pointer_cast<ast::Exp>($3), // Right operand
                    ast::BinOpType::DIV
                );
            }
            | ID { $$ = std::dynamic_pointer_cast<ast::ID>($1); }
            | Call { $$ = std::dynamic_pointer_cast<ast::Call>($1); }
            | NUM { $$ = std::dynamic_pointer_cast<ast::Num>($1); }
            | NUM_B { $$ = std::dynamic_pointer_cast<ast::NumB>($1); }
            | STRING { $$ = std::dynamic_pointer_cast<ast::String>($1); }
            | TRUE { $$ = std::dynamic_pointer_cast<ast::Bool>($1); }
            | FALSE { $$ = std::dynamic_pointer_cast<ast::Bool>($1); }
            | NOT Exp {
                $$ = std::make_shared<ast::Not>(
                    std::dynamic_pointer_cast<ast::Exp>($2)
                );
            }
            | Exp AND Exp {
                $$ = std::make_shared<ast::And>(
                    std::dynamic_pointer_cast<ast::Exp>($1),
                    std::dynamic_pointer_cast<ast::Exp>($3)
                );
            }
            | Exp OR Exp {
                $$ = std::make_shared<ast::Or>(
                    std::dynamic_pointer_cast<ast::Exp>($1),
                    std::dynamic_pointer_cast<ast::Exp>($3)
                );
            }
            | Exp RELOP Exp {
                auto relop = std::dynamic_pointer_cast<ast::RelOp>($2); // Extract the BinOp object
                $$ = std::make_shared<ast::RelOp>(
                    std::dynamic_pointer_cast<ast::Exp>($1), // Left operand
                    std::dynamic_pointer_cast<ast::Exp>($3), // Right operand
                    relop->op // Extract the operation type
                );
            }
            | Exp EQ Exp {
                $$ = std::make_shared<ast::RelOp>(
                    std::dynamic_pointer_cast<ast::Exp>($1), // Left operand
                    std::dynamic_pointer_cast<ast::Exp>($3), // Right operand
                    ast::RelOpType::EQ
                );
            }
            | Exp NE Exp {
                $$ = std::make_shared<ast::RelOp>(
                    std::dynamic_pointer_cast<ast::Exp>($1), // Left operand
                    std::dynamic_pointer_cast<ast::Exp>($3), // Right operand
                    ast::RelOpType::NE
                );
            } 
            | LPAREN Type RPAREN Exp {
                $$ = std::make_shared<ast::Cast>(
                    std::dynamic_pointer_cast<ast::Exp>($4),
                    std::dynamic_pointer_cast<ast::Type>($2)
                );
            };


%%

// Error Handling
void yyerror(const char * message) {
    output::errorSyn(yylineno);
    exit(0);
}